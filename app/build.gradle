apply plugin: 'com.android.application'

def versionMajor = 0
def versionMinor = 6
def versionPatch = 2
def globalSuffix = ""

ext.getVersionCode = { ->
    return versionMajor * 10000 + versionMinor * 100 + versionPatch
}

ext.getVersionName = { ->
    return "${versionMajor}.${versionMinor}.${versionPatch}${globalSuffix}"
}

android {

    signingConfigs {
        release {
        }
    }

    compileSdkVersion 33
    buildToolsVersion "33.0.0"

    defaultConfig {
        applicationId "prototype.xd.scheduler"
        minSdkVersion 26
        targetSdkVersion 32
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}${globalSuffix}"
        signingConfig signingConfigs.debug
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            versionNameSuffix '.release'
            resValue "string", "app_version_combined", "Scheduler v${defaultConfig.versionName}.release (${defaultConfig.versionCode})"
        }
        debug {
            renderscriptDebuggable true
            jniDebuggable true
            debuggable true
            versionNameSuffix '.debug'
            resValue "string", "app_version_combined", "Scheduler v${defaultConfig.versionName}.debug (${defaultConfig.versionCode})"
        }

    }

    buildFeatures {
        viewBinding = true
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_11
        sourceCompatibility JavaVersion.VERSION_11
    }

    namespace 'prototype.xd.scheduler'
    sourceSets {
        main {
            res {
                srcDirs 'src/main/res',
                        'src/main/res/layouts/calendar',
                        'src/main/res/layouts/lockscreen_views',
                        'src/main/res/layouts/partials',
                        'src/main/res/layouts/full_views',
                        'src/main/res/layouts/settings',
                        'src/main/res/layouts/dialogs'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation "androidx.lifecycle:lifecycle-service:2.5.1"

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'

    implementation 'com.github.QuadFlask:colorpicker:0.0.15'
    implementation 'com.kizitonwose.calendar:view:2.1.1'
    implementation 'com.github.AppIntro:AppIntro:6.2.0'
    implementation 'org.dmfs:lib-recur:0.12.2'

    implementation "com.google.guava:guava:31.0.1-android"

    //debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'
}

task getVersionCode() {
    // only run during execution, not evaluation
    doLast {
        println getVersionCode()
    }
}

task getVersionName() {
    // only run during execution, not evaluation
    doLast {
        println getVersionName()
    }
}

